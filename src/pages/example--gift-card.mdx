import { Callout } from "nextra-theme-docs";

# Gift Card

Let's build a UI to send and redeem a gift card using smart contracts on Cardano.

## Covered in this tutorial

- writing `Aiken`
- parameterizing scripts
- using [lucid](https://lucid.spacebudz.io/)
- using [deno fresh](https://fresh.deno.dev/)

<Callout type="info">
  â˜… You should install deno using these
  [instructions](https://deno.land/manual@v1.29.1/getting_started/installation).
</Callout>

## What is a gift card?

In the context of this tutorial a gift card will involve locking some assets
in a smart contract. While some assets are being locked, we'll mint an NFT in
the same transaction. This NFT could be sent anywhere and the owner of the NFT
can burn it to unlock the assets that were previously locked. We can consider
the NFT as a gift card.

## Aiken is the easy part

Let's go ahead and create a new `Aiken` project:

```sh
aiken new my-org/gift-card
cd gift-card
```

<Callout type="info">
  `my-org` above can be replaced by any name. We recommend using the name of a
  Github organization or your own username.
</Callout>

We've already covered what `aiken new` generates in a previous tutorial so
let's jump right into some code.

Go ahead and remove the `lib/` folder, we won't be needing that for this tutorial.

```
rm -rf lib
```

Now let's create a new file in the `validators/` folder called `oneshot.ak`.

```
touch validators/oneshot.ak
```

<Callout type="info">
  `oneshot.ak` could be named anything. Any file in `validators/` is allowed to
  export as many validators as you'd like.
</Callout>

Now let's open the project folder in our favorite editor and define two empty
validator functions.

```aiken filename="validators/oneshot.ak"
use aiken/transaction.{ScriptContext}

validator {
  fn gift_card(rdmr, ctx: ScriptContext) -> Bool {
    todo @"mint and burn"
  }
}

validator {
  fn redeem(_d: Data, _r: Data, ctx: ScriptContext) -> Bool {
    todo @"redeem"
  }
}
```

The `gift_card` validator will be used to mint and burn the gift card. The
`redeem` validator will be used with `gift_card` to redeem the gift card and unlock the assets.
The life cycle of this gift card will involve two transactions. The first transaction
will mint the gift card as an NFT and it will send some assets to the `redeem` validator's address.
The gift card can be sent anywhere in the first transaction. The second transaction will
burn the NFT and send the locked assets to the address that held the burned NFT.

### Minting a Gift Card

Since this example is for a oneshot minting contract let's add
some parameters to the validator that we can use to guarantee uniqueness.

```aiken filename="validators/oneshot.ak" /OutputReference/
use aiken/transaction.{OutputReference, ScriptContext}

validator(token_name: ByteArray, utxo_ref: OutputReference) {
  fn gift_card(rdmr, ctx: ScriptContext) -> Bool {
    todo @"mint and burn"
  }
}
```

We'll use the `utxo_ref` parameter to ensure this validator will only allow a mint once. Since
utxos are cryptographically guaranteed to be unique we can use this parameter for this purpose.

Next let's define a type for `rdmr`. We have two actions that this validator
will perform. This validator can be used to mint and then burn an NFT.

```aiken filename="validators/oneshot.ak" {3-6,10-15} /Action/2
use aiken/transaction.{OutputReference, ScriptContext}

type Action {
  Mint
  Burn
}

validator(token_name: ByteArray, utxo_ref: OutputReference) {
  fn gift_card(rdmr: Action, ctx: ScriptContext) -> Bool {
    when rdmr is {
      Mint ->
        todo @"mint"
      Burn ->
        todo @"burn"
    }
  }
}
```

Next we'll do these things in order so that we have everything we need
to perfom the final check.

- pattern match on the script context to get `transaction` and `purpose`
- `expect` the `purpose` to be `tx.Mint(policy_id)`
- pattern match on the `transaction` to get it's `inputs` and `mint` which holds minted assets
- `expect` minted assets (`mint`) to only have one item which has an `asset_name` and an `amount`

```aiken filename="validators/oneshot.ak" {1,3,12-21} /Transaction/1
use aiken/dict
use aiken/transaction.{OutputReference, ScriptContext, Transaction} as tx
use aiken/transaction/value

type Action {
  Mint
  Burn
}

validator(token_name: ByteArray, utxo_ref: OutputReference) {
  fn gift_card(rdmr: Action, ctx: ScriptContext) -> Bool {
    let ScriptContext { transaction, purpose } = ctx

    expect tx.Mint(policy_id) = purpose

    let Transaction { inputs, mint, .. } = transaction

    expect [(asset_name, amount)] =
      mint
      |> value.tokens(policy_id)
      |> dict.to_list()

    when rdmr is {
      Mint ->
        todo @"mint"
      Burn ->
        todo @"burn"
    }
  }
}
```

At this point we have all the data we need to perform the final check for the `Mint` action.
For this validator to succeed we need to ensure that the `utxo_ref` parameter equals one of
the `inputs` in the transaction. In addition to this, we need to ensure `amount` is equal to one because
we're minting an NFT. For fun, we'll check that `asset_name` is equal to `token_name` from the parameters.

```aiken filename="validators/oneshot.ak" {2,26-28}
use aiken/dict
use aiken/list
use aiken/transaction.{OutputReference, ScriptContext, Transaction} as tx
use aiken/transaction/value

type Action {
  Mint
  Burn
}

validator(token_name: ByteArray, utxo_ref: OutputReference) {
  fn gift_card(rdmr: Action, ctx: ScriptContext) -> Bool {
    let ScriptContext { transaction, purpose } = ctx

    expect tx.Mint(policy_id) = purpose

    let Transaction { inputs, mint, .. } = transaction

    expect [(asset_name, amount)] =
      mint
      |> value.tokens(policy_id)
      |> dict.to_list()

    when rdmr is {
      Mint ->
        expect Some(_input) =
          list.find(inputs, fn(input) { input.output_reference == utxo_ref })
        amount == 1 && asset_name == token_name
      Burn ->
        todo @"burn"
    }
  }
}
```

We have everything we need in this validator to mint a Gift Card. Before
we start making transactions though, we'll need to finish the `Burn` action and that
will also be paired with the `redeem` validator.

### Redeeming a Gift Card

To redeem a gift card we'll want a transaction that
uses two validators. We'll use the `gift_card` validator with
the `Burn` action to burn the NFT. We'll also use the `redeem` validator
to unlock the assets at that address.

Let's finish the `Burn` action of the `gift_card` validator. We just need
to check that `amount` is equal to negative one and that `asset_name` is equal to `token_name`.

```aiken filename="validators/oneshot.ak" {30}
use aiken/dict
use aiken/list
use aiken/transaction.{OutputReference, ScriptContext, Transaction} as tx
use aiken/transaction/value

type Action {
  Mint
  Burn
}

validator(token_name: ByteArray, utxo_ref: OutputReference) {
  fn gift_card(rdmr: Action, ctx: ScriptContext) -> Bool {
    let ScriptContext { transaction, purpose } = ctx

    expect tx.Mint(policy_id) = purpose

    let Transaction { inputs, mint, .. } = transaction

    expect [(asset_name, amount)] =
      mint
      |> value.tokens(policy_id)
      |> dict.to_list()

    when rdmr is {
      Mint ->
        expect Some(_input) =
          list.find(inputs, fn(input) { input.output_reference == utxo_ref })
        amount == 1 && asset_name == token_name
      Burn ->
        amount == -1 && asset_name == token_name
    }
  }
}
```

Now we can start working on the `redeem` validator. We'll want to parameterize this validator
with `token_name` and the `policy_id` which is the hash of the `gift_card` validator after having it's
params applied. We parameterize `redeem` with `gift_card`'s `policy_id` so that we can only use `redeem`
exclusively with `gift_card`.

```aiken filename="validators/oneshot.ak" {8}
use aiken/dict
use aiken/list
use aiken/transaction.{OutputReference, ScriptContext, Transaction} as tx
use aiken/transaction/value

// ... gift card validator ...

validator(token_name: ByteArray, policy_id: ByteArray) {
  fn redeem(_d: Data, _r: Data, ctx: ScriptContext) -> Bool {
    todo @"redeem"
  }
}
```

Let's add some boilerplate to this validator so that we can get the
`asset_name` and the `amount` out of the script context's transaction.

```aiken filename="validators/oneshot.ak" {10-17}
use aiken/dict
use aiken/list
use aiken/transaction.{OutputReference, ScriptContext, Transaction} as tx
use aiken/transaction/value

// ... gift card validator ...

validator(token_name: ByteArray, policy_id: ByteArray) {
  fn redeem(_d: Data, _r: Data, ctx: ScriptContext) -> Bool {
    let ScriptContext { transaction, .. } = ctx

    let Transaction { mint, .. } = transaction

    expect [(asset_name, amount)] =
      mint
      |> value.tokens(policy_id)
      |> dict.to_list()

    todo @"redeem"
  }
}
```

Finally we need to confirm that `asset_name` is equal to `token_name`
and that `amount` is equal to negative one.

```aiken filename="validators/oneshot.ak" {19}
use aiken/dict
use aiken/list
use aiken/transaction.{OutputReference, ScriptContext, Transaction} as tx
use aiken/transaction/value

// ... gift card validator ...

validator(token_name: ByteArray, policy_id: ByteArray) {
  fn redeem(_d: Data, _r: Data, ctx: ScriptContext) -> Bool {
    let ScriptContext { transaction, .. } = ctx

    let Transaction { mint, .. } = transaction

    expect [(asset_name, amount)] =
      mint
      |> value.tokens(policy_id)
      |> dict.to_list()

    amount == -1 && asset_name == token_name
  }
}
```

We should make sure this builds. You've been running `aiken check` along the way right?!?

<Callout type="info">
  Jokes aside, you're probably using an editor integration. If the editor
  integration isn't giving you proper feed back or giving you a hard time please
  come talk to us so we can make things better.
</Callout>

```sh
aiken build
```

## Building a frontend

With the easy part out of the way we can start building a frontend to interact with our
smart contracts in the browser. Deno fresh is an interesting project for building
web applications in Deno.

### Setting up

Let's generate a Deno fresh project in the same directory as our Aiken project.

```sh
deno run -A -r https://fresh.deno.dev .
```

<Callout type="warning">When prompted to enable Tailwind CSS say yes.</Callout>

We need lucid and we should probably add an alias for better looking imports.
Let's edit `import_map.json`.

```json filename="import_map.json" {11-12}
{
  "imports": {
    "$fresh/": "...",
    "preact": "...",
    "preact/": "...",
    "preact-render-to-string": "...",
    "@preact/signals": "...",
    "@preact/signals-core": "...",
    "twind": "...",
    "twind/": "...",
    "lucid/": "https://deno.land/x/lucid@0.9.5/",
    "~/": "./"
  }
}
```

We can delete a few things that come with the starter template that we don't need.

```sh
rm islands/Counter.tsx
rm -rf routes/api
rm routes/\[name\].tsx
```

### Home page

Everything we'll be doing with validators and transactions will happen fully client side.
This means we can just have our route render a single `island` component and then
we can write all of our code in this island for the most part.

Let's create a new file `islands/Oneshot.tsx` and add the following code.

```tsx filename="islands/Oneshot.tsx"
export default function Oneshot() {
  return <div>Oneshot</div>;
}
```

Now inside of `routes/index.tsx` we can import our new island and render it.

```tsx filename="routes/index.tsx" {3,29}
import { Head } from "$fresh/runtime.ts";

import Oneshot from "~/islands/Oneshot";

export default function Home() {
  return (
    <>
      <Head>
        <title>One Shot</title>
      </Head>

      <div class="max-w-2xl mx-auto mt-20 mb-10">
        <div class="mb-10">
          <h2 class="text-lg font-semibold text-gray-900">
            Make a one shot minting and lock contract
          </h2>

          <h3 class="mt-4 mb-2">Redeem</h3>
          <pre class="bg-gray-200 p-2 rounded overflow-x-scroll">
            TODO: Render non-parameterized redeem validator
          </pre>

          <h3 class="mt-4 mb-2">Gift Card</h3>
          <pre class="bg-gray-200 p-2 rounded overflow-x-scroll">
            TODO: Render non-parameterized gift_card validator
          </pre>
        </div>

        <Oneshot />
      </div>
    </>
  );
}
```

You can replace everything that was in `routes/index.tsx` with the above code.
We've left some `TODO`'s in the code to remind us to render the validators. We'll render
the compiled aiken code as a hex encoded string. There not much of a reason to do this, it's just
kinda cool to see.

Next we should load the `plutus.json` file and get the compiled aiken code. Let's create
a file called `utils.ts` and add the following code.

```ts filename="utils.ts"
import { MintingPolicy, SpendingValidator } from "lucid/mod.ts";

import blueprint from "~/plutus.json" assert { type: "json" };

export type Validators = {
  redeem: SpendingValidator;
  giftCard: MintingPolicy;
};

export function readValidators(): Validators {
  const redeem = blueprint.validators.find((v) => v.title === "oneshot.redeem");

  if (!redeem) {
    throw new Error("Redeem validator not found");
  }

  const giftCard = blueprint.validators.find(
    (v) => v.title === "oneshot.gift_card"
  );

  if (!giftCard) {
    throw new Error("Gift Card validator not found");
  }

  return {
    redeem: {
      type: "PlutusV2",
      script: redeem.compiledCode,
    },
    giftCard: {
      type: "PlutusV2",
      script: giftCard.compiledCode,
    },
  };
}
```

There's nothing particularly special here. We're just reading the `plutus.json` file
and finding the compiled code for the `redeem` and `gift_card` validators. We're also
exporting a type for the validators so we can use it in our island later. Having this function
potentially throw an error is just a way to signal to us that we've done something wrong.

Let's import our new `readValidators` file into our `routes/index.tsx` file and use it to
in a server side handler. This will allow us to access the data in the `Home` page component
as page props which we'll then use to render the validator's compiled code.

```tsx filename="routes/index.tsx" {2,5,7-9,11-17} /validators/4-8 /PageProps<Data>/ /data/
import { Head } from "$fresh/runtime.ts";
import { Handlers, PageProps } from "$fresh/server.ts";

import Oneshot from "~/islands/Oneshot";
import { readValidators, Validators } from "~/utils.ts";

interface Data {
  validators: Validators;
}

export const handler: Handlers<Data> = {
  GET(_req, ctx) {
    const validators = readValidators();

    return ctx.render({ validators });
  },
};

export default function Home({ data }: PageProps<Data>) {
  const { validators } = data;

  return (
    <>
      <Head>
        <title>One Shot</title>
      </Head>

      <div class="max-w-2xl mx-auto mt-20 mb-10">
        <div class="mb-10">
          <h2 class="text-lg font-semibold text-gray-900">
            Make a one shot minting and lock contract
          </h2>

          <h3 class="mt-4 mb-2">Redeem</h3>
          <pre class="bg-gray-200 p-2 rounded overflow-x-scroll">
            {validators.redeem.script}
          </pre>

          <h3 class="mt-4 mb-2">Gift Card</h3>
          <pre class="bg-gray-200 p-2 rounded overflow-x-scroll">
            {validators.giftCard.script}
          </pre>
        </div>

        <Oneshot validators={validators} />
      </div>
    </>
  );
}
```

Your editor will probably complain and say that `Oneshot` doesn't accept a `validators` prop.
We'll fix that in a moment.

### The island

It's about time we start the real party and we've made it to the juicy part. In this island
we'll capture some user input, apply some params to our raw validators, and execute some transactions.
To keep things simple we'll assume [eternl](https://eternl.io/) is setup in your browser. Another thing
we'll do to keep things simple is have the gift card be sent to ourselves when minted. This way we
can test the redeeming of the gift card without having to send it to someone else or using a second wallet.

#### Params
