import { Callout } from "nextra-theme-docs";

# Gift Card

Let's build a UI to send and redeem a gift card using smart contracts on Cardano.

## Covered in this tutorial

- writing `Aiken`
- parameterizing scripts
- using [lucid](https://lucid.spacebudz.io/)
- using [deno fresh](https://fresh.deno.dev/)

<Callout type="info">
  â˜… You should install deno using these
  [instructions](https://deno.land/manual@v1.29.1/getting_started/installation).
</Callout>

## What is a gift card?

In the context of this tutorial a gift card will involve locking some assets
in a smart contract. While some assets are being locked, we'll mint an NFT in
the same transaction. This NFT could be sent anywhere and the owner of the NFT
can burn it to unlock the assets there were previously locked. We can consider
the NFT as a gift card.

## Aiken is the easy part

Let's go ahead and create a new `Aiken` project:

```sh
aiken new my-org/gift-card
cd gift-card
```

<Callout type="info">
  `my-org` above can be replaced by any name. We recommend using the name of a
  Github organization or your own username.
</Callout>

We've already covered what `aiken new` generates in a previous tutorial so
let's jump right into some code.

Go ahead and remove the `lib/` folder, we won't be needing that for this tutorial.

```
rm -rf lib
```

Now let's create a new file in the `validators/` folder called `main.ak`.

```
touch validators/main.ak
```

<Callout type="info">
  `main.ak` could be named anything. Any file in `validators/` is allowed to
  export as many validators as you'd like.
</Callout>

Now let's open the project folder in our favorite editor and define two empty
validator functions.

```aiken filename="validators/main.ak"
use aiken/transaction.{ScriptContext}

validator {
  fn gift_card(rdmr, ctx: ScriptContext) -> Bool {
    todo @"mint and burn"
  }
}

validator {
  fn redeem(d, r, ctx: ScriptContext) -> Bool {
    todo @"redeem"
  }
}
```

The `gift_card` validator will be used to mint and burn the NFT. The
`redeem` validator will be used to redeem the NFT and unlock the assets.

### Minting a Gift Card

Since this example is for a oneshot minting contract let's add
some parameters to the validator that we can use to guarantee uniqueness.

```aiken filename="validators/main.ak" /OutputReference/
use aiken/transaction.{OutputReference, ScriptContext}

validator(token_name: ByteArray, utxo_ref: OutputReference) {
  fn gift_card(rdmr, ctx: ScriptContext) -> Bool {
    todo @"mint and burn"
  }
}
```

We'll use the `utxo_ref` parameter to ensure this validator will only allow a mint once. Since
utxos are cryptographically guaranteed to be unique we can use this parameter for this purpose.

Next let's define a type for `rdmr`. We have two actions that this validator
will perform. This validator can be used to mint and then burn an NFT.

```aiken filename="validators/main.ak" {3-6,10-15} /Action/2
use aiken/transaction.{OutputReference, ScriptContext}

type Action {
  Mint
  Burn
}

validator(token_name: ByteArray, utxo_ref: OutputReference) {
  fn gift_card(rdmr: Action, ctx: ScriptContext) -> Bool {
    when rdmr is {
      Mint ->
        todo @"mint"
      Burn ->
        todo @"burn"
    }
  }
}
```

Next we'll do these things in order so that we have everything we need
to perfom the final check.

- pattern match on the script context to get `transaction` and `purpose`
- `expect` the `purpose` to be `tx.Mint(policy_id)`
- pattern match on the `transaction` to get it's `inputs` and `mint` which holds minted assets
- `expect` minted assets (`mint`) to only have one item which has an `asset_name` and an `amount`

```aiken filename="validators/main.ak" {1,3,12-21} /Transaction/1
use aiken/dict
use aiken/transaction.{OutputReference, ScriptContext, Transaction} as tx
use aiken/transaction/value

type Action {
  Mint
  Burn
}

validator(token_name: ByteArray, utxo_ref: OutputReference) {
  fn gift_card(rdmr: Action, ctx: ScriptContext) -> Bool {
    let ScriptContext { transaction, purpose } = ctx

    expect tx.Mint(policy_id) = purpose

    let Transaction { inputs, mint, .. } = transaction

    expect [(asset_name, amount)] =
      mint
      |> value.tokens(policy_id)
      |> dict.to_list()

    when rdmr is {
      Mint ->
        todo @"mint"
      Burn ->
        todo @"burn"
    }
  }
}
```

At this point we have all the data we need to perform the final check in the `Mint` branch.
For this validator to succeed we need to ensure that the `utxo_ref` parameter equals one of
the `inputs` in the transaction. In addition to this, we need to ensure `amount` is equal to one because
we're minting an NFT. For fun, we'll check that `asset_name` is equal to `token_name` from the parameters.

```aiken filename="validators/main.ak" {2,26-28}
use aiken/dict
use aiken/list
use aiken/transaction.{OutputReference, ScriptContext, Transaction} as tx
use aiken/transaction/value

type Action {
  Mint
  Burn
}

validator(token_name: ByteArray, utxo_ref: OutputReference) {
  fn gift_card(rdmr: Action, ctx: ScriptContext) -> Bool {
    let ScriptContext { transaction, purpose } = ctx

    expect tx.Mint(policy_id) = purpose

    let Transaction { inputs, mint, .. } = transaction

    expect [(asset_name, amount)] =
      mint
      |> value.tokens(policy_id)
      |> dict.to_list()

    when rdmr is {
      Mint ->
        expect Some(_input) =
          list.find(inputs, fn(input) { input.output_reference == utxo_ref })
        amount == 1 && asset_name == token_name
      Burn ->
        todo @"burn"
    }
  }
}
```

We have everything we need in this validator to mint a Gift Card. Before
we start making transactions though, we'll need to finish the `Burn` branch and that
will also be paired with the `redeem` validator.

### Redeeming a Gift Card
